
# PIG Example

**Find the oldest 5-star movies**

Let's jump right into the pig script.

Again: this is instead of MR.

## 1) load data

``` pig
ratings = LOAD '/user/maria_dev/ml-100k/u.data' AS 
    (userID:int, movieID:int, rating:int, ratingTime:int)
```

Ratings is a relation.

**LOAD** is a function that loads the data, and places it to our HDFS cluster.

AS is the schema. It is applied when the data is read in.

We can load plain text data, and use AS to assign ints and strs, etc.

**This creates a relation named "ratings" with a given schema defined by a series of tuples with 4 elements each with specific types**

This would have been our MAPPER in a MAP REDUCE.

# PigStorage

We also want to load the u.item file which has all the movie info.

u.item is a bit different as it has pipe delimeters - but we can deal with that.

We can use the LOAD command and create a new relation

``` pig
metadata = LOAD '/user/maria_dev/ml-100k/u.item' USING
    PigStorage('|')AS (movieID:int, movieTitle:chararray, 
    releaseDate:chararray, videoRelease:chararray,
    imdbLink:chararray);

DUMP metadata;
```

# Sort Release Date

We need to make the release date in a format that can be sorted.

We are going to use a FOREACH generate - which will generate a new relation

``` pig
nameLookup = FOREACH metadata GENERATE movieID, movieTitle,
    ToUnixTime(ToDate(releaseDate, 'dd-MMM-yyyy')) AS releaseTime;
```

So now we have a new relation with the column releaseTime.

We also discarded the IMDB because we didn't need it.

# Group By

We need to know all the ratings associate for each individual movie - this is what GROUP BY does.

``` pig
ratingsByMovie = GROUP ratings BY movieID;
DUMP ratingsByMovie;
```

This creates a bag, which is tuples of all the individuals rows associated with a given ID.

So this is a REDUCER operation.

## Compute ratings

To compute the ratings we can use a FOREACH

``` pig
avgRatings = FOREACH ratingsByMovie GENERATE group as movieID, 
    AVG(ratings.rating) AS avgRating;

DUMP avgRatings
```

## DESCRIBE

If you want to see what each expressions do, you can use DESCRIBE

``` pig
DESCRIBE ratings;
DESCRIBE ratingsByMovie;
DESCRIBE avgRatings;
```

## FILTER

Now we just want to be left with only movies ratesd with 5 stars

``` pig
fiveStarMovies = FILTER avgRatings BY avgRating > 4.0;
```

## JOIN

``` pig
fiveStarsWithData = JOIN fiveStarMovies by movieID, nameLookup by movieID;
```

This is much easier than doing it if MR. 

## ORDER BY

Last thing is to just order the data!

``` pig
oldestFiveStarMovies = ORDER fiveStarsWithData BY
    nameLookup::releaseTime;

DUMP oldestFiveStarMovies;
```

# Putting it all together

``` pig
ratings = LOAD '/user/maria_dev/ml-100k/u.data' AS 
    (userID:int, movieID:int, rating:int, ratingTime:int)

metadata = LOAD '/user/maria_dev/ml-100k/u.item' USING
    PigStorage('|')AS (movieID:int, movieTitle:chararray, 
    releaseDate:chararray, videoRelease:chararray,
    imdbLink:chararray);

nameLookup = FOREACH metadata GENERATE movieID, movieTitle,
    ToUnixTime(ToDate(releaseDate, 'dd-MMM-yyyy')) AS releaseTime;

ratingsByMovie = GROUP ratings BY movieID;

avgRatings = FOREACH ratingsByMovie GENERATE group as movieID, 
    AVG(ratings.rating) AS avgRating;

fiveStarMovies = FILTER avgRatings BY avgRating > 4.0;

fiveStarsWithData = JOIN fiveStarMovies by movieID, nameLookup by movieID;

oldestFiveStarMovies = ORDER fiveStarsWithData BY
    nameLookup::releaseTime;

DUMP oldestFiveStarMovies;
```

# Time To Run it

We'll enter it in ambari




