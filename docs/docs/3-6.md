
# PIG Challenge

**Find the most popular bad movies!**

## Defining the problem

* Find all movies with an average rating less than 2.0
* Sort them by the total number of ratings

## Hint

* We used everything you need in our earlier example of finding old movies with ratings greater than 4.0
* Only new thing you need is COUNT(). 

## Answer

``` pig
ratings = LOAD '/user/maria_dev/ml-100k/u.data' AS 
    (userID:int, movieID:int, rating:int, ratingTime:int);

metadata = LOAD '/user/maria_dev/ml-100k/u.item' USING
    PigStorage('|')AS (movieID:int, movieTitle:chararray, 
    releaseDate:chararray, videoRelease:chararray,
    imdbLink:chararray);

nameLookup = FOREACH metadata GENERATE movieID, movieTitle;

ratingsByMovie = GROUP ratings BY movieID;

avgRatings = FOREACH ratingsByMovie GENERATE group as movieID, 
    AVG(ratings.rating) AS avgRating, COUNT(ratings.rating) AS countRating;

twoStarMovies = FILTER avgRatings BY avgRating < 2.0;

twoStarsWithData = JOIN twoStarMovies by movieID, nameLookup by movieID;

popularTwoStarMovies = ORDER twoStarsWithData BY
    twoStarMovies::countRating 
    DESC;

DUMP popularTwoStarMovies;
```


